# For an example of how to make your own CMake project that uses OpenSim,
# see SampleCMakeLists.txt. Do NOT use find_package(Simbody) in your own
# project; this script will take care of that for you. If you *really* want to
# use a different Simbody than the one with which OpenSim was compiled, 
# set(OPENSIM_SEPARATE_SIMBODY ON) TODO
#
# This config file defines the following variables:
#   @CMAKE_PROJECT_NAME@_FOUND               True if search for libraries and headers
#                               succeeds.
#   @CMAKE_PROJECT_NAME@_ROOT_DIR            The installation directory.
#   @CMAKE_PROJECT_NAME@_LIB_DIR             The directory containing libraries.
#                               Suitable for link_directories().
#   @CMAKE_PROJECT_NAME@_INCLUDE_DIRS        Suitable for
#   include_directories(). TODO  tell pple not to use this.
#   @CMAKE_PROJECT_NAME@_LIBRARIES           Suitable for target_link_libraries().
#
# Note that these variables also contain the Simbody include directories
# and libraries that OpenSim was built with.
#
# Adapted from SimbodyConfig.cmake
#

# To make the OpenSim installation relocatable:
@PACKAGE_INIT@

set_and_check(@CMAKE_PROJECT_NAME@_ROOT_DIR "@PACKAGE_CMAKE_INSTALL_PREFIX@")

# This holds the value of OPENSIM_COPY_SIMBODY from when OpenSim was built.
set(_SIMBODY_IS_COPIED_INTO_OPENSIM @OPENSIM_COPY_SIMBODY@)

# LIB_DIR
# -------
if(WIN32)
    set(_OPENSIM_LIB_RELPATH "sdk/lib")
else()
    set(_OPENSIM_LIB_RELPATH "lib")
endif()

set_and_check(@CMAKE_PROJECT_NAME@_LIB_DIR
    "@PACKAGE_CMAKE_INSTALL_PREFIX@/${_OPENSIM_LIB_RELPATH}")

if(NOT ${_SIMBODY_IS_COPIED_INTO_OPENSIM})
    list(APPEND @CMAKE_PROJECT_NAME@_LIB_DIR "@Simbody_LIB_DIR@")
endif()

# INCLUDE_DIRS
# ------------
list(APPEND @CMAKE_PROJECT_NAME@_INCLUDE_DIRS
    "@PACKAGE_CMAKE_INSTALL_PREFIX@/sdk/include"
    "@PACKAGE_CMAKE_INSTALL_PREFIX@/sdk/include/Vendors/lepton/include"
    )

if(${_SIMBODY_IS_COPIED_INTO_OPENSIM})
    # Simbody headers have been copied to OpenSim.
    if(WIN32)
        set(_SIMBODY_INCLUDE_RELPATH "include")
    else()
        set(_SIMBODY_INCLUDE_RELPATH "simbody")
    endif()
    # TODO obey OPENSIM_SEPARATE_SIMBODY
    list(APPEND @CMAKE_PROJECT_NAME@_INCLUDE_DIRS
        "@PACKAGE_CMAKE_INSTALL_PREFIX@/sdk/include/SimTK/${_SIMBODY_INCLUDE_RELPATH}")
else()
    # Refer to Simbody's own headers.
    list(APPEND @CMAKE_PROJECT_NAME@_INCLUDE_DIRS "@Simbody_INCLUDE_DIR@")
endif()

# LIBRARIES
# ---------
include("@PACKAGE_OPENSIM_INSTALL_CMAKE_DIR@/@CMAKE_PROJECT_NAME@Targets.cmake")

# The osimTools target uses all the other targets.
# This makes a distinction between release and debug libraries.
list(APPEND @CMAKE_PROJECT_NAME@_LIBRARIES osimTools)

check_required_components(OpenSim)

# Simbody
# -------
# The files OpenSimTargets-debug.cmake, OpenSimTargets-release.cmake, etc.
# list the Simbody libraries as dependencies by name (e.g., `SimTKmath`).
# Consider a CMake project that uses OpenSim: If CMake doesn't know of any
# targets named `SimTKmath`, then CMake will assume that `SimTKmath` is the
# name of a library, *not of a target*, and will add `-lSimTKmath` to the link
# line and assume that the library is on the (DY)LD_LIBRARY_PATH or in a
# directory mentioned in a call to `link_directories()`. However, if you've
# only built debug libraries, then there is no libSimTKmath.so (on Linux); there
# is only a libSimTKmath_d.so.
# So, we better tell CMake about a SimTKmath target.
# The way we do so depends on how OpenSim was built.
# TODO don't do any of this if the user wants to use a separate Simbody.
if(NOT ${_SIMBODY_IS_COPIED_INTO_OPENSIM})
    # If Simbody libraries/headers were *not* copied into OpenSim, then we
    # simply import Simbody's targets using find_package().
    find_package(Simbody @SIMBODY_VERSION_TO_USE@ REQUIRED
        PATHS "@Simbody_ROOT_DIR@"
        NO_MODULE NO_DEFAULT_PATH)
else()
    # If we copied over Simbody libraries/header into the OpenSim installation,
    # then we assume that the machine on which this file is being read does
    # *not* have a standalone installation of Simbody. We must manually create
    # IMPORTED targets to represent the Simbody libraries that come with
    # OpenSim.
    # TODO windows: SONAME is unnecessary.
    # TODO IMPLIB
    # TODO Linux
    # TODO RPATH
    @OPENSIM_COPY_SIMBODY_IMPORTED_TARGETS@
endif()
