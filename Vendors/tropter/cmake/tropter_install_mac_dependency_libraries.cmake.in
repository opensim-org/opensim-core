# Temporary hack to package dependencies on Macs.

set(libdir "@CMAKE_INSTALL_PREFIX@/@CMAKE_INSTALL_LIBDIR@")
message(STATUS "Editing RPATH of tropter dependency libraries")

macro(install_name_tool_change lib dep_name dep_oldpath)
    execute_process(COMMAND install_name_tool
            -change ${dep_oldpath}/lib${dep_name}.dylib
            @rpath/lib${dep_name}.dylib
            "${libdir}/lib${lib}.dylib")
endmacro()

macro(install_name_tool_change_gfortran lib)
    execute_process(COMMAND bash "-c"
        "otool -L ${libdir}/lib${lib}.dylib | grep 'libgfortran.*version' | perl -pe 's/^\\s+(.*)\\/libg.*/\\1/gc'"
        OUTPUT_VARIABLE libgfortran_dir
        OUTPUT_STRIP_TRAILING_WHITESPACE)
    execute_process(COMMAND bash "-c"
        "otool -L ${libdir}/lib${lib}.dylib | grep 'libgfortran.*version' | perl -pe 's/.*lib(.*).dylib.*/\\1/gc'"
        OUTPUT_VARIABLE libgfortran_name
        OUTPUT_STRIP_TRAILING_WHITESPACE)
    install_name_tool_change(${lib} "${libgfortran_name}" "${libgfortran_dir}")
endmacro()

macro(install_name_tool_change_quadmath lib)
execute_process(COMMAND bash "-c"
    "otool -L ${libdir}/lib${lib}.dylib | grep 'libquadmath' | perl -pe 's/^\\s+(.*)\\/libq.*/\\1/gc'"
    OUTPUT_VARIABLE libquadmath_dir
    OUTPUT_STRIP_TRAILING_WHITESPACE)

execute_process(COMMAND bash "-c"
    "otool -L ${libdir}/lib${lib}.dylib | grep 'libquadmath' | perl -pe 's/.*lib(.*).dylib.*/\\1/gc'"
    OUTPUT_VARIABLE libquadmath_name
    OUTPUT_STRIP_TRAILING_WHITESPACE)
    install_name_tool_change(${lib} "${libquadmath_name}" "${libquadmath_dir}")
endmacro()

macro(install_name_tool_id lib)
    execute_process(COMMAND install_name_tool
            -id @rpath/lib${lib}.dylib
            "${libdir}/lib${lib}.dylib")
endmacro()

macro(install_name_tool_add_rpath lib)
    execute_process(COMMAND install_name_tool
            -add_rpath "@loader_path/"
            "${libdir}/lib${lib}.dylib")
endmacro()

macro(install_name_tool_delete_rpath lib rpath)
    execute_process(COMMAND install_name_tool
            -delete_rpath ${rpath}
            "${libdir}/lib${lib}.dylib")
endmacro()

# Get the directory containing libgcc_s.1.dylib.
# otool: Get the list of dependencies of coinmumps, which includes libgcc_s.
# grep: Isolate the line containing libgcc.
# perl: Extract only the directory, without the filename.
#    ^\\s+     matches all spaces at the start of the string.
#   (.*)       matches everything up to libgcc, and captures it as match gorup #1
#   \\/libg.*  matches the rest of the line.
#   \\1        replaces the contents of the entire match with match group #1
execute_process(COMMAND bash "-c"
    "otool -L ${libdir}/libcoinmumps.dylib | grep 'libgcc' | perl -pe 's/^\\s+(.*)\\/libg.*/\\1/gc'"
    OUTPUT_VARIABLE libgcc_dir
    OUTPUT_STRIP_TRAILING_WHITESPACE)

# Get the actual name of the library libgcc_s.1.dylib (can be libgcc_s.1.1.dylib),
# or another if it changes in the future.
execute_process(COMMAND bash "-c"
    "basename `otool -L ${libdir}/libcoinmumps.dylib  | grep 'libgcc' | awk '{print $1}'` .dylib | cut -c 4-"
    OUTPUT_VARIABLE libgcc_name
    OUTPUT_STRIP_TRAILING_WHITESPACE)

# tropter
install_name_tool_change(tropter adolc.2 "@ADOLC_DIR@/lib64")
install_name_tool_change(tropter ipopt.3 "@IPOPT_LIBDIR@")

# adol-c
install_name_tool_id(adolc.2)
install_name_tool_add_rpath(adolc.2)
install_name_tool_delete_rpath(adolc.2 "@ColPack_ROOT_DIR@/lib")

# ipopt
install_name_tool_id(ipopt.3)
install_name_tool_change(ipopt.3 coinmumps.3 "@IPOPT_LIBDIR@")
install_name_tool_change(ipopt.3 metis "@IPOPT_LIBDIR@")
install_name_tool_change_gfortran(ipopt.3)
install_name_tool_change_quadmath(ipopt.3)

# coinmumps
install_name_tool_id(coinmumps.3)
install_name_tool_change(coinmumps.3 metis "@IPOPT_LIBDIR@")
install_name_tool_change(coinmumps.3 "${libgcc_name}" "${libgcc_dir}")
install_name_tool_add_rpath(coinmumps.3)
install_name_tool_change_gfortran(coinmumps.3)
install_name_tool_change_quadmath(coinmumps.3)

# metis
install_name_tool_id(metis)
install_name_tool_add_rpath(metis)
 

